#include "hsp3dish.as"
#include "beautifulsoup.hsp"

#module
#deffunc 棋譜取得 str dirname, int max

	dim result
	now_dir = dir_cur
	
	url = "https://katagotraining.org"
	url2 = url + "/games"
	
	chdir dirname
	dirlist files,"*.sgf"

	httpload url2
	if stat : goto *bad	; 正しくリクエストができなかった
	gosub*通信待ち
	httpinfo buf,HTTPINFO_DATA
	httpinfo size,HTTPINFO_SIZE

	find_tags_bytag buf,"table", result
	find_tags_bytag result,"a", r
	find_attribute_byname r.1, "href", f

	httpload url + f
	if stat: goto*bad
	gosub *通信待ち
	httpinfo buf,HTTPINFO_DATA
	httpinfo size,HTTPINFO_SIZE

	find_tags_bytag buf,"tr", data //テーブルの要素
	

	repeat limit(max,,length(data)-1)
	
		find_tags_bytag data(cnt + 1) ,"td", target_infos //テーブルの対象の要素をリスト化(見出し（最初のtr）は関係ないので[(cnt + 1)]

		game_id = int(get_content_tag(target_infos(0))) //ゲームIDを取得

		if check_exist_sgf(files, game_id): continue

		find_tags_bytag data(cnt + 1),"a", result
		find_attribute_byname result, "href", t

		httpload url + "/" + t
		if stat: goto*bad
		gosub *通信待ち
		httpinfo buf,HTTPINFO_DATA
		httpinfo size,HTTPINFO_SIZE
		
		bsave dirname + "/" + game_id + ".sgf", buf, size

		await 300

	loop
	


	chdir now_dir
	return

*通信待ち
	;	結果待ちのためのループ
	httpinfo res,HTTPINFO_MODE
	if res = HTTPMODE_READY : return
	if res <= HTTPMODE_NONE : goto *bad
	await 40
	goto *通信待ち

*bad
	;	エラー
	httpinfo estr, HTTPINFO_ERROR
	dialog "ERROR "+estr
	stop

#defcfunc check_exist_sgf str s1, int p1

	k = s1
	notesel k

	hit  =0
	repeat notemax
		noteget t, cnt
		if int(t) = p1{
			hit = 1
		}
	loop
	
	noteunsel


	return hit
	
#global