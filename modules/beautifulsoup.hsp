#module 

#deffunc find_tags_bytag str s1, str tag, array result, int start

// find_tags_bytag 対象の文字列, 対象のタグ, 結果を受け取る配列, オフセット(0)

 //現状<a href="">など　<のすぐ後にタグ名や閉じタグ名がある場合のみ対応。
 //< a href="">とかだと動作しない。
 //必要になれば対応する。
 
	point = start
	s = s1
	sdim result
	repeat
		p = instr(s,point,"<" + tag) //タグ検索
		if p < 0: break
		left = point + p
		point += p + strlen(tag) + 2
		p2 = instr(s,point,"</"+ tag + ">")　//終了タグ検索
		
		if p2 < 0{
			
			//まずタグを登録　"<>の形式になってなければ終了"
			tmp = instr(s, left, ">")
			if tmp < 0: break
			
			//閉じタグはないタグ（<br>など）として登録
			result(cnt) = strmid(s, point - strlen(tag) - 2, tmp + 2)
			point += strlen(result(cnt))
			p = instr(s, point, "<" + tag)
			if p >= 0: continue
			
		}else{
			point += p2 + strlen(tag) + 3
			//閉じタグまでを登録 <div> ----- </div>
			result(cnt) = strmid(s, left, point - left)
		}
	loop
	return
	
#deffunc find_attribute_byname str s1, str name, array result
	s = s1
	point = -1
	token_start = 0 ; トークン開始位置
	mode = 2       ; 0 = 属性名取得中, 1 = 属性値取得中, 2 = タグ名取得中
	mode2 = 0       ; 0 = トークン開始位置検索中, 1 = トークン開始済み
	
	sdim result
	
	repeat
		point++
		if point >= strlen(s): break
		a = peek(s, point)

		token_str = strmid(s, token_start, point-token_start)

		switch a
		case '<'
		swbreak
		case '>'
		case ' '
		case '='
			if mode = 0{;属性名
				if a = '>'{
					mode = 2
				}
				if a == ' ' || a == '='{
					if mode2 = 1{
						if name == token_str{
							mode = 1
						}
					}
				}
			}else: if mode == 2{;タグ名
				mode = 0
			}else: if mode == 1{;属性値
				if mode2{
					if mode == 1{
						result = token_str
						break
					}
					mode = 0
				}
			}
			mode2 = 0
		swbreak
		case '\''
		case '"'
			if mode = 1{
				t = " "
				poke t,0,a
				mlen = instr(s, point + 1, t)
				if mlen < 0: dialog "シングルもしくはダブルクォートが閉じられていません": end
				if mode == 1{
					result = strmid(s,point+1,mlen)
					break
				}
				point += mlen + 2
			}
			
		swbreak
		default
			if (a >='a' && a <= 'z') || (a >= 'A' &&  a <= 'Z') || (a >='0' && a <= '9') || (a=='-') || (a=='.') || (a=='_') || (a ==':'){
				if mode2 = 0{
					token_start = point
					mode2 = 1
				}
			}
		swbreak
		swend
	loop
	return
	
#defcfunc get_content_tag str s1
	r = ""
	s = s1
	left =  instr(s,0,">") + 1
	if left{
		l = instr(s,left,"<") 
		if l >= 0{
			r = strmid(s,left,l)
		}
		
	}
	return r
#global


#if 0

a={"
 <div id="main" class="container" data-role="page">
    <p title = "greeting">Hello, World!</p>
    <a href= "?search?query=go&lang=en" target="_blank">Visit Example</a>
    <img src= 'image.png' alt="Sample Image">
    <input type= text>
 </div>
"}


find_attribute_byname a,"id",b
find_attribute_byname a,"class",c
find_attribute_byname a,"title",d
find_attribute_byname a,"href",e
find_attribute_byname a,"src",f
find_attribute_byname a,"type",g
find_attribute_byname a,"nonexistent",h


mes b
mes c
mes d
mes e
mes f
mes g
mes h

#endif