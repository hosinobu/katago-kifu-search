#ifndef sgf_node
#module sgf_node

#define KEY_VALUE_SEP "|||"

#define MAX_NODE 1500000

#deffunc init_sgf_module
	dim parent, MAX_NODE //親ノード
	
	dim sgf_node_turn, MAX_NODE //ノードの手番
	dim sgf_node_x, MAX_NODE //ノードの座標ｘ
	dim sgf_node_y, MAX_NODE //ノードの座標ｙ
	sdim sgf_properties_data,, MAX_NODE //その他のプロパティ
	dim sgf_properties_num,MAX_NODE //その他のプロパティの数

	sdim sgf_node_childs,, MAX_NODE //子ノードリスト
	dim child_num, MAX_NODE //子ノードの数

	//------------------------------------
	sdim open_id_list, 1024 * 4 //配列の未使用番号リスト
	lpoke open_id_list, 0, 0
	open_id_list_num = 1 //配列の未使用番号の数
	node_count = 1 //配列のサイズ（未使用要素も含む）

	SEP = " "
	poke SEP, 0, $1F
	
	return

#defcfunc make_new_SGFnode int p1
	if open_id_list_num{
		open_id_list_num--
		new_id = lpeek(open_id_list, open_id_list_num * 4)
	}else{
		new_id = node_count
		node_count++
	}

	init_node new_id, p1
	
	return new_id

#deffunc add_sgf_property int node_id, str s1, str s2

	sgf_properties_data(node_id) += s1 + KEY_VALUE_SEP + s2 + SEP
	sgf_properties_num(node_id)++
	
	return
#deffunc del_sgf_property int node_id, str s1

	dup data, sgf_properties_data(node_id)
	
	index = instr(data, 0, SEP + s1 + KEY_VALUE_SEP)
	if index == -1: return
	
	next_index = index + 1 + instr(data, index + 1, SEP)

	left = strmid(data, 0, index)
	right = strmid(data, next_index, 999999)

	data = left + right

	sgf_properties_num(node_id)--

	return

#deffunc add_SGFchild int node_id, int p1
    if search_SGFchild(node_id, p1) == -1 {
        index = child_num(node_id) * 4  // 4バイト単位で格納
        memexpand sgf_node_childs(node_id), index + 4
        lpoke sgf_node_childs(node_id), index, p1
        child_num(node_id)++
    }
    return

#deffunc del_sgf_node int node_id, local child
	repeat get_sgf_childs(node_id, child)
		del_sgf_node(lpeek(child, cnt * 4))
	loop
	lpoke open_id_list, open_id_list_num * 4, node_id
	open_id_list_num++
	init_node node_id, -1
	return

#deffunc set_property_move int node_id, int turn, int x, int y
	sgf_node_turn(node_id) = turn
	sgf_node_x(node_id) = x
	sgf_node_y(node_id) = y
	return
	
#deffunc set_property_data int node_id, str s, str d
	if s == "B"{
		tmp = d
		set_property_move node_id, 1, peek(tmp, 0) - 'a', peek(tmp, 1) - 'a'
	}else: if s == "W"{
		tmp = d
		set_property_move node_id, 2, peek(tmp, 0) - 'a', peek(tmp, 1) - 'a'
	}else{
		//登録
		del_sgf_property node_id, s
		add_sgf_property node_id, s, d
	}
	return

#defcfunc get_property_data int node_id, str property_name
	dup data, sgf_properties_data(node_id)
	
	// プロパティの名前と値を探す
	index = instr(data, 0, SEP + property_name + KEY_VALUE_SEP)
	if index == -1: return ""  // 見つからなかった場合は空文字を返す
	
	// データの最後までのインデックスを取得
	next_index = index + 1 + instr(data, index + 1, SEP) //最後のSEPは必ず存在する事はモジュール全体で保証。
	
	value_start = index + 1 + strlen(property_name) + strlen(KEY_VALUE_SEP) //+1はstrlen(SEP)
	value_end = next_index

	// プロパティの値を抽出して返す
	result = strmid(data, value_start, value_end - value_start)
	
	;logmes "@@@@" + result
	return result

#defcfunc get_property_coordinate_cash int node_id, var x, var y
	x = sgf_node_x(node_id)
	y = sgf_node_y(node_id)
	return sgf_node_turn(node_id)

#defcfunc get_SGF_childs int node_id, array a
	dup a, sgf_node_childs(node_id)
	return child_num(node_id)

#defcfunc search_SGFchild int node_id, int target
	result = -1
    repeat child_num(node_id)
        if lpeek(sgf_node_childs(node_id), cnt * 4) == target{
	    	result = cnt
	    	break
	    }
    loop
    return result

#defcfunc search_SGFchild_byxy int node_id, int _x, int _y, int turn, local child

	result = -1
	repeat get_SGF_childs(node_id, child)
		if turn == get_property_coordinate_cash(lpeek(child, cnt * 4), x, y){
			if _x == x && _y == y{
				result = lpeek(child, cnt * 4): break
			}
		}
	loop
	
	return result


#defcfunc get_node_sgf int p1
	result = "("
	_get_node_sgf p1, result
	result += ")"
	return result

#deffunc _get_node_sgf int node_id, var s, local i, local n, local child

	s += ";"
	
	if sgf_node_turn(node_id) != -1{
		座標 = "  "
		poke 座標, 0, 'a' + sgf_node_x(node_id)
		poke 座標, 1, 'a' + sgf_node_y(node_id)
		
		if sgf_node_turn(node_id) == 1{
			s += "B[" + 座標 + "]"
		}else{
			s += "W[" + 座標 + "]"
		}
	}

	split sgf_properties_data(node_id), SEP, tmp
	repeat sgf_properties_num(node_id), 1
		split tmp(cnt), KEY_VALUE_SEP, kv
		if kv == "AB" || kv = "AW"{
			s += kv
			repeat strlen(kv.1) / 2
				s += "[" + strmid(kv.1,cnt*2,2) + "]"
			loop
		}else{
			s += kv + "[" + kv.1 + "]"
		}
	loop

	n = get_SGF_childs(node_id, child)
	for i,, n
	
		if n >= 2{
			s+="("
		}
		
		_get_node_sgf lpeek(child, i * 4), s
		
		if n >= 2{
			s+=")"
		}
	next

	return

#deffunc get_nodelist int node_id, array a
	dim a
	_get_nodelist node_id, a
	return
#deffunc _get_nodelist int node_id, array a, int num
	a(num) = node_id
	if parent(node_id) != -1{
		_get_nodelist parent(node_id), a, num + 1
	}
	return

//特定のノードまでのSGFを取得
#defcfunc get_node_sgf_nowpoint int p1, local i
	get_nodelist p1, a
	result = "("
	for i, length(a)-1,-1,-1
		result += get_node_sgf_nowpoint_main(a(i))
	next
	result += ")"
	return result
	
#defcfunc get_node_sgf_nowpoint_main int node_id, local i
	i = ";"
	if sgf_node_turn(node_id) != -1{
		座標 = "  "
		poke 座標, 0, 'a' + sgf_node_x(node_id)
		poke 座標, 1, 'a' + sgf_node_y(node_id)
		if sgf_node_turn(node_id) == 1{
			i += "B[" + 座標 + "]"
		}else{
			i += "W[" + 座標 + "]"
		}	
	}

	split sgf_properties_data(node_id), SEP, tmp
	repeat sgf_properties_num(node_id), 1
		split tmp(cnt), KEY_VALUE_SEP, kv
		if kv == "AB" || kv == "AW"{
			i += kv
			repeat strlen(kv.1)/2
				i += "[" + strmid(kv.1,cnt*2,2) + "]"
			loop
		}else{
			i += kv + "[" + kv.1 + "]"
		}
	loop
	return i

#deffunc get_board_size_from_SGF int SGF_topnode, array xy

	size = get_property_data(SGF_topnode, "SZ")
	if size == ""{
		logmes "SGFにボードサイズが設定されていません"
		xy = 19,19
	}else{
		split size,":", x, y
		if stat = 1: y = x
		xy = int(x), int(y)
	}
	return
#defcfunc fetch_or_create_SGFnode int nownode, array coordinate, int 手番

	if nownode >= 0{
		if search_SGFchild_byxy(nownode, coordinate, coordinate.1 , 手番) >= 0{
			return stat //すでにノードが存在
		}
	}
	
	new = make_new_SGFnode(nownode)
	
	if nownode >= 0{
		add_SGFchild nownode, new
	}
	
	set_property_move new, 手番, coordinate, coordinate.1
		
	return new
#deffunc local init_node int node_id, int p1

	parent(node_id) = p1
	
	sgf_node_turn(node_id) = -1
	sgf_node_x(node_id) = -1
	sgf_node_y(node_id) = -1
	
	sgf_properties_data(node_id) = SEP //SEPで始まりSEPで終わる形式で保存 (splitしたら、先頭と末尾は空になることに注意)
	sgf_properties_num(node_id) = 0
	
	sgf_node_childs(node_id) = ""
	child_num(node_id) = 0

	return
#global
init_sgf_module

#module
#defcfunc 対応する文字まで取得 var ss, int index, str s1, var result
	i = instr(ss, index, s1)
	result = strmid(ss, index, i)
	return index + i
#global

#module
#define global ctype load_sgf(%1, %2 = -1) _load_sgf(%1, %2)
#defcfunc _load_sgf str ss, int node
	
	sss = ss
	sgflen = strlen(sss)
	if sgflen < 3{
		//最小SGFは　(;)
		return -1
	}
	if (peek(sss) != '(') || (peek(sss, sgflen - 1) != ')'){
		return -1
	}
	start_point = instr(sss, 0 ,";")
	if start_point == -1{
		return -1
	}
	
	topnode = make_new_SGFnode(node)
	load_sgf_main strmid(sss, start_point + 1, sgflen - start_point - 2), topnode
	
	return topnode

#deffunc load_sgf_main str ss, int node
	sgfs = ss
	sgf_length = strlen(sgfs)
	nn = node
	point = 0
	//解析
	while point < sgf_length
		
		token = peek(sgfs, point)

		if (token == ';'){
			
			new_id = make_new_SGFnode(nn)
			add_SGFchild nn, new_id
			nn = new_id
			
		}else:if token >= 'A' && token <= 'Z'{
			
			point = 対応する文字まで取得(sgfs, point, "[", P)
			point = 対応する文字まで取得(sgfs, point + 1, "]", D)
			set_property_data nn, P, D
			
		}else:if token == '['{
			
			point = 対応する文字まで取得(sgfs, point + 1, "]", D)
			set_property_data nn, P, get_property_data(nn, P) + D
			
		}else:if token = '('{
			
			stack(pn) = nn
			pn++
			
		}else:if token == ')'{
			
			pn--
			nn = stack(pn)
			
		}else{
			
			logmes "不正なトークンが含まれています"
			
		}
		point++
	wend
	return
#global
#endif


#if 0
//testcode by chatgpt

dim coord, 2
coord(0) = 3  // 'd' の座標
coord(1) = 4  // 'e' の座標

// ルートノード作成
root = make_new_SGFnode(-1)

test1 = make_SGFnode(root, coord, 1) // 黒の手
set_property_data test1, "C", "First move"

test2 = make_SGFnode(test1, coord, 2) // 白の手 (同じ座標)
set_property_data test2, "C", "Second move"

test3 = make_SGFnode(test2, coord, 1) // 黒の手
set_property_data test3, "C", "Third move"

// 子ノードの検索
found = search_SGFchild(root, test1)
if found != -1 {
    mes "Child node found: " + found
} else {
    mes "Child node not found"
}

// プロパティの取得
data1 = get_property_data(test1, "C")
data2 = get_property_data(test2, "C")
data3 = get_property_data(test3, "C")
mes "Node 1 comment: " + data1
mes "Node 2 comment: " + data2
mes "Node 3 comment: " + data3

// SGF 形式への変換
sgf_data = get_node_sgf(root)
mes "Generated SGF: " + sgf_data

// SGF の読み込み
top_node = load_sgf(sgf_data)
if top_node != -1 {
    mes "SGF loaded successfully."
} else {
    mes "Failed to load SGF."
}

#endif